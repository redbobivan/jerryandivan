//Qlik View Script

/*
The script is used to deal with missing dimensions in a fact table.

@param 1: dTableName: the name of the dimension table
@param 2: fTableName: the name of the fact table
@param 3: keyColumn: the key column in the fact table
@param 4: dimKeyColumn: the key column in the dimension table
@param 5: dColumnsNeedMissingLabel: the columns in the dimension table that need to be filled with missing label
@param 6: vMissinglabelPrefix: the prefix of the missing label

@syntax Call DealWithMissingDimTable('Dimension', 'Fact', '%CustomerID', 'Customer_ID', 'Dimension.Customer,Dimension.Country', 'Unknow');
*/

Sub DealWithMissingDimTable(dTableName, fTableName, keyColumn, dimKeyColumn, dColumnsNeedMissingLabel, vMissinglabelPrefix)
    
    unqualify *;
    NullAsValue $(dColumnsNeedMissingLabel);
    Set NullValue = $(vMissinglabelPrefix);
    
    tmp_MissingColumns:
    Load subfield('$(dColumnsNeedMissingLabel)',',')as MissingColumn
	AutoGenerate(1);
    
    Set wMissingColumnStatement =;
    Let NumRows = NoOfRows('tmp_MissingColumns');
    For i=0 to $(NumRows) -1
    	Let wMissingColumn=Peek('MissingColumn',$(i));
        wMissingColumnStatement = wMissingColumnStatement&' ''$(vMissinglabelPrefix) for $(keyColumn) '' & $(keyColumn) as $(wMissingColumn),';
    Next;
    wMissingColumnStatement = Left(wMissingColumnStatement, Len(wMissingColumnStatement) - 1)
    Trace -->$(wMissingColumnStatement);
    
	Concatenate ($(dTableName))
  	Load Distinct
      $(keyColumn),
      $(keyColumn) As $(dTableName).$(dimKeyColumn),
      $(wMissingColumnStatement),
      'True' as QaFlag
      
  	Resident
		$(fTableName)
  	Where Len(Lookup('$(keyColumn)', '$(keyColumn)', $(keyColumn), '$(dTableName)'))=0;
    
    Qualify *;
	Unqualify '%*';
    
End Sub

//Example
Qualify *;
Unqualify '%*';

Dimension:
Load *, CustomerID as '%CustomerID' Inline [
CustomerID, Customer, Country
1, Customer A, USA
2, Customer B, USA
3, Customer C, UK
4, Customer D, UK
];


Fact:
Load *, CustomerID as '%CustomerID' Inline [
Date, CustomerID, Sales Value
2014-01-01, 1, 100
2014-01-01, 2, 100
2014-01-01, 3, 100
2014-01-01, 4, 100
2014-01-01, 5, 100
2014-01-02, 1, 100
2014-01-02, 2, 100
2014-01-02, 4, 100
2014-01-03, 5, 300
];

//Dealing with missing or late arriving dimension values
Call DealWithMissingDimTable('Dimension', 'Fact', '%CustomerID', 'Customer_ID', 'Dimension.Customer,Dimension.Country', 'Unknow');

Exit Script;
